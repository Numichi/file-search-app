plugins {
    id 'java'
    id 'jacoco' // For code coverage
    id 'org.springframework.boot' version '3.5.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.15.0' // For OpenAPI code generation
}

group = 'com.github.numichi'
version = '0.0.1-SNAPSHOT'
description = 'demo10'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}

repositories {
    mavenCentral()
}

configurations {
    configureEach {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }

    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.bouncycastle:bcpkix-jdk18on:1.81' // For Argon2
    implementation 'org.passay:passay:1.6.6' // For password policy

    // UUID generation tool (contains UUIDv7)
    implementation 'com.github.f4b6a3:uuid-creator:6.1.1'

    // Logging
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.25.1' // For SLF4J bridge
    implementation 'com.lmax:disruptor:4.0.0' // For Async logging with Log4j2

    // MapStruct and Lombok
    implementation 'org.projectlombok:lombok'
    annotationProcessor "org.projectlombok:lombok:1.18.34"
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"

    // OpenAPI
    implementation "io.swagger.core.v3:swagger-annotations:2.2.38"

    // Database Migration
    implementation 'org.liquibase:liquibase-core'
    runtimeOnly 'org.postgresql:postgresql'

    // Jackson Support
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.7'

    // Tests - core and security
    testImplementation 'org.springframework.boot:spring-boot-starter-test' // For testing core
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    // Test - REST Assured tools
    testImplementation 'io.rest-assured:rest-assured:5.5.6' // For testing REST APIs
    testImplementation 'io.rest-assured:spring-mock-mvc:5.5.6' // For testing Spring MVC controllers
    // Test - Testcontainers for integration tests
    testImplementation 'org.springframework.boot:spring-boot-testcontainers' // Testcontainers integration base
    testImplementation 'org.testcontainers:junit-jupiter' // JUnit 5 Testcontainers support
    testImplementation 'org.testcontainers:postgresql' // PostgreSQL Testcontainer
    // Test - Lombok
    testCompileOnly 'org.projectlombok:lombok:1.18.34'
    testAnnotationProcessor "org.projectlombok:lombok:1.18.34"
    // Test - other tools
    testImplementation 'net.datafaker:datafaker:2.4.4' // For generating fake data
    testImplementation 'org.instancio:instancio-junit:5.5.1' // For generating random test data
}

tasks.named('compileJava') {
    dependsOn tasks.named('openApiGenerate')
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

//=============================================================================
// Jacoco & Test coverage
//=============================================================================

tasks.jacocoTestReport {
    dependsOn test

    reports {
        xml.required.set(true)
        html.required.set(true)
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

//=============================================================================
// OpenAPI Generator
//=============================================================================

openApiGenerate {
    generatorName = "spring"
    inputSpec = "${rootProject.rootDir}/src/main/resources/openapi/openapi.yaml"
    outputDir = layout.buildDirectory.dir("generated").get().asFile.absolutePath
    apiPackage = "com.github.numichi.generated.openapi.api"
    invokerPackage = "com.github.numichi.generated.openapi.invoker"
    modelPackage = "com.github.numichi.generated.openapi.model"
    configOptions = [
            "useTags"              : "true",
            "generateBuilders"     : "true",
            "interfaceOnly"        : "true",
            "skipDefaultInterface" : "true",
            "booleanGetterPrefix"  : "is",
            "useSpringBoot3"       : "true",
            "documentationProvider": "springdoc",
    ]
}